apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

codeQualityTools {
  checkstyle {
    toolVersion = '7.7'
  }
  pmd {
    toolVersion = '5.6.1'
  }
}

buildscript {
  repositories {
    jcenter()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.4.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.4.0'

    classpath 'com.android.tools.build:gradle:2.4.0-alpha7'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.10'
  }
}

codeQualityTools {
  lint {
    textReport true
  }
}

junitJacoco {
  ignoreProjects = ['app', 'emoji-ios', 'emoji-google', 'emoji-one'] // Don't care about sample and generated modules.
}

subprojects {
  repositories {
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME

  plugins.apply('net.ltgt.errorprone')

  configurations.errorprone {
    resolutionStrategy.force 'com.google.errorprone:error_prone_core:2.0.19'
  }
}

ext {
  minSdkVersion = 15
  compileSdkVersion = 25
  targetSdkVersion = 25
  buildToolsVersion = '25.0.2'

  supportLibraryVersion = '25.3.1'
  leakCanaryVersion = '1.5.1'

  versionCode = VERSION_CODE
  versionName = VERSION_NAME

  supportAppCompat = "com.android.support:appcompat-v7:$supportLibraryVersion"
  supportV4CoreUi = "com.android.support:support-core-ui:$supportLibraryVersion"
  supportV4Compat = "com.android.support:support-compat:$supportLibraryVersion"
  supportRecyclerView = "com.android.support:recyclerview-v7:$supportLibraryVersion"
  supportCardView = "com.android.support:cardview-v7:$supportLibraryVersion"

  leakCanaryDebug = "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
  leakCanaryRelease = "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

  testing = [junit             : 'junit:junit:4.12',
             robolectric       : 'org.robolectric:robolectric:3.3.2',
             mockito           : 'org.mockito:mockito-core:2.8.9',
             privateConstructor: 'com.pushtorefresh.java-private-constructor-checker:checker:1.2.0',
             assertJ           : 'org.assertj:assertj-core:3.6.2']

  javaVersion = JavaVersion.VERSION_1_7
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.5'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}
